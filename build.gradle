buildscript {
    ext {
        springBootVersion = '2.0.6.RELEASE'
        gradleLombokPluginVersion = '1.14'
        gradleGitPluginVersion = '2.0.0-beta1'
        gradleDockerPluginVersion = '0.20.1'
    }
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.franzbecker:gradle-lombok:${gradleLombokPluginVersion}")
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:$gradleGitPluginVersion"
        classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:$gradleDockerPluginVersion"
    }
}

subprojects {

    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: "com.gorylenko.gradle-git-properties"
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.palantir.docker'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.11

    repositories {
        jcenter()
    }

    lombok {
        version = "1.18.2"
    }

    springBoot {
        buildInfo()
    }

    docker {
        name "${project.group}/${bootJar.baseName}"
        copySpec.from("build/libs").into("libs")
        buildArgs([JAR_FILE: "libs/$bootJar.archiveName"])
    }

    dependencies {
        implementation('org.springframework.boot:spring-boot-starter-actuator')
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
        testImplementation('org.junit.jupiter:junit-jupiter-api')
        testImplementation('de.flapdoodle.embed:de.flapdoodle.embed.mongo')
    }

}
